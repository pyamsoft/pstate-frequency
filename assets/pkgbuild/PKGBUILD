# Maintainer: Peter Yamanaka <pyam.soft@gmail.com>

pkgname=pstate-frequency-git
_gitname=pstate-frequency
pkgdesc="Easily control Intel p-state driver on Linux "
pkgver=0.r36.e856eb4
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('git')
provides=('pstate-frequency')
license=('GPLv2')
url="https://github.com/pyamsoft/pstate-frequency.git"
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    printf "0.r%s.%s" \
        "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_gitname"
    # Variables with their defaults below
    # CC=gcc
    # DEBUG=0
    make\
        CC="gcc" \
        DEBUG=0
}

package() {
    cd "$srcdir/$_gitname"
    # Variables with their defaults below
    # CC="gcc"
    # PREFIX="/usr/local/"
    # INCLUDE_BASH_COMPLETION=0
    # INCLUDE_UDEV_RULE=0
    make DESTDIR="$pkgdir" \
        CC="gcc" \
        PREFIX="/usr/" \
        INCLUDE_BASH_COMPLETION=0 \
        INCLUDE_UDEV_RULE=0 \
        install
}
