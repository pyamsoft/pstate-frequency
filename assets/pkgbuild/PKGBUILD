# Maintainer: pyamsoft <pyam.soft@gmail.com>

# Varialbes which hold the name of the git repository
# and the branch to clone
_gitname=pstate-frequency
_gitbranch=master
pkgname=pstate-frequency-git
pkgdesc="Easily control Intel p-state driver on Linux "
pkgver=1.r93.d868941
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('git')
optdepends=(
	'msr-tools: Allow explicit setting of Turbo Boost bit on Sandy Bride+ CPUs'
)
provides=('pstate-frequency')
license=('GPLv2')
url="https://github.com/pyamsoft/pstate-frequency.git"
source=("${_gitname}::git+${url}#branch=${_gitbranch}")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	printf "1.r%s.%s" \
		"$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

##################################################

	# Please read config.mk for an explanation
	# of the variable options in this file

# DEFAULTS 

# _CC="cc"
# _PREFIX="/usr/local"
# _ETCDIR="/etc"
# _WRITE_MSR=0
# _INCLUDE_BASH_COMPLETION=1
# _INCLUDE_ZSH_COMPLETION=1
# _INCLUDE_UDEV_RULE=0

_CC="cc"
_PREFIX="/usr"
_ETCDIR="/etc"
_WRITE_MSR=0
_INCLUDE_BASH_COMPLETION=1
_INCLUDE_ZSH_COMPLETION=1
_INCLUDE_UDEV_RULE=1

##################################################

build() {
	cd "$srcdir/$_gitname"
	make\
		CC="${_CC}" \
		INCLUDE_UDEV_RULE=${_INCLUDE_UDEV_RULE} \
		WRITE_MSR=${_WRITE_MSR}
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" \
		CC="${_CC}" \
		PREFIX="${_PREFIX}" \
		ETCDIR="${_ETCDIR}" \
		INCLUDE_BASH_COMPLETION=$_INCLUDE_BASH_COMPLETION \
		INCLUDE_ZSH_COMPLETION=${_INCLUDE_ZSH_COMPLETION} \
		INCLUDE_UDEV_RULE=${_INCLUDE_UDEV_RULE} \
		install
}
