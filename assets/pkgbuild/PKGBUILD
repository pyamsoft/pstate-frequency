# Maintainer: Peter Yamanaka <pyam.soft@gmail.com>

pkgname=pstate-frequency-git
_gitname=pstate-frequency
pkgdesc="Easily control Intel p-state driver on Linux "
pkgver=0.r58.a561672
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('git')
optdepends=(
    'msr-tools: Allow explicit setting of Turbo Boost bit on Sandy Bride+ CPUs'
)
provides=('pstate-frequency')
license=('GPLv2')
url="https://github.com/pyamsoft/pstate-frequency.git"
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    printf "0.r%s.%s" \
        "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_gitname"
    # Variables with their defaults below
    # CC=cc
    # DEBUG=0
    # WRITE_MSR=1
    make\
        CC="cc" \
        DEBUG=0 \
        WRITE_MSR=1
}

package() {
    cd "$srcdir/$_gitname"
    # Variables with their defaults below
    # CC=cc
    # PREFIX=/usr/local
    # ETCDIR=/etc
    # INCLUDE_BASH_COMPLETION=0
    # INCLUDE_UDEV_RULE=0
    make DESTDIR="$pkgdir" \
        CC="cc" \
        PREFIX="/usr" \
        ETCDIR="/etc" \
        INCLUDE_BASH_COMPLETION=0 \
        INCLUDE_UDEV_RULE=0 \
        install
}
