# Maintainer: pyamsoft <pyam.soft@gmail.com>

pkgname=pstate-frequency-git
_gitname=pstate-frequency
pkgdesc="Easily control Intel p-state driver on Linux "
pkgver=1.r83.e1a482c
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('git')
optdepends=(
	'msr-tools: Allow explicit setting of Turbo Boost bit on Sandy Bride+ CPUs'
)
provides=('pstate-frequency')
license=('GPLv2')
url="https://github.com/pyamsoft/pstate-frequency.git"
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	printf "1.r%s.%s" \
		"$(git rev-list --count HEAD)" \
		"$(git rev-parse --short HEAD)"
}

##################################################

	# Please read config.mk for an explanation
	# of the variable options in this file

##################################################

build() {
	cd "$srcdir/$_gitname"
	# Variables with their defaults below
	# CC=cc
	# WRITE_MSR=0
	make\
		CC="cc" \
		WRITE_MSR=0
}

package() {
	cd "$srcdir/$_gitname"
	# Variables with their defaults below
	# CC=cc
	# PREFIX=/usr/local
	# ETCDIR=/etc
	# INCLUDE_BASH_COMPLETION=1
	# INCLUDE_ZSH_COMPLETION=1
	# INCLUDE_UDEV_RULE=0
	make DESTDIR="$pkgdir" \
		CC="cc" \
		PREFIX="/usr" \
		ETCDIR="/etc" \
		INCLUDE_BASH_COMPLETION=1 \
		INCLUDE_ZSH_COMPLETION=1 \
		INCLUDE_UDEV_RULE=0 \
		install
}
